plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.kezong.fat-aar'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding = true
        viewBinding true
    }
}

afterEvaluate {
    // for app test, copy the final aar to application libs
    android.libraryVariants.all { variant ->
        File outputFile = variant.outputs.first().outputFile
        tasks.named("assemble${variant.name.capitalize()}").configure {
            doLast {
                copy {
                    from outputFile
                    into "../app/libs"
                    rename outputFile.name, "fat-aar-final-timesheet.aar"
                }
            }
        }
    }
}

fataar {
    /**
     * If transitive is true, local jar module and remote library's dependencies will be embed.
     * If transitive is false, just embed first level dependency
     * Local aar project does not support transitive, always embed first level
     * Default value is false
     * @since 1.3.0
     */
    transitive = false
}

dependencies {
    implementation(project(":common"))

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.google.dagger:hilt-android:2.41'
    implementation 'androidx.hilt:hilt-navigation-fragment:1.0.0'
    kapt 'com.google.dagger:hilt-compiler:2.41'

    embed project(path: ':common', configuration: 'default')
    embed project(path: ':config', configuration: 'default')
    embed project(path: ':extension', configuration: 'default')
    embed project(path: ':network', configuration: 'default')
    embed project(path: ':utility', configuration: 'default')
}